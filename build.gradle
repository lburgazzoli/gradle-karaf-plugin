
// *****************************************************************************
//
// *****************************************************************************

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    id 'signing'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.ben-manes.versions' version '0.15.0'
    id "io.codearte.nexus-staging" version "0.9.0"
}

// *****************************************************************************
//
// *****************************************************************************

group               = 'com.github.lburgazzoli'
description         = 'A gradle plugin for Karaf'
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

ext {
    isReleaseVersion  = !version.endsWith("SNAPSHOT")
    isSnapshotVersion = version.endsWith("SNAPSHOT")
    isCI              = Boolean.valueOf("$System.env.CI")
    isPR              = "false" != System.env.TRAVIS_PULL_REQUEST 
    gitRoot           = "https://github.com/lburgazzoli"
    gitProject        = "https://github.com/lburgazzoli/gradle-karaf-plugin"
    gitURL            = "git@github.com:lburgazzoli/gradle-karaf-plugin"
    groovyVersion     = GroovySystem.version.replaceAll(/\.\d+$/,'')
    gradleVersion     = '3.0'
    gradleScriptDir   = "${rootProject.projectDir}/gradle"
    gradlePluginId    = "com.github.lburgazzoli.karaf"
    gradlePluginTags  = [ 'karaf' ]
    gradleCiTasks     = (isReleaseVersion || isPR) ? ['clean', 'test'] : ['clean', 'test', 'uploadArchives']

    versions = [
        slf4j  : '1.7.21',
        paxUrl : '2.5.2',
        spock  : "1.1-groovy-${groovyVersion}"
    ]

    if (isReleaseVersion) {
        nexusUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    } else {
        nexusUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    if (!project.hasProperty('nexusUsername') && !project.hasProperty('nexusPassword')) {
        nexusUsername = "$System.env.CI_DEPLOY_USERNAME"
        nexusPassword = "$System.env.CI_DEPLOY_PASSWORD"
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile "org.ops4j.pax.url:pax-url-aether:$versions.paxUrl"

    testCompile("org.spockframework:spock-core:$versions.spock") {
        exclude(module: 'groovy-all')
    }
}

project.tasks.withType(org.gradle.api.tasks.compile.JavaCompile) {
    it.sourceCompatibility = project.sourceCompatibility
    it.targetCompatibility = project.targetCompatibility
}

project.tasks.withType(org.gradle.api.tasks.compile.GroovyCompile) {
    it.sourceCompatibility = project.sourceCompatibility
    it.targetCompatibility = project.targetCompatibility
}

jar {
    baseName = "${project.name}"

    manifest {
        attributes['Implementation-Title'   ] = "${group}.${project.name}-${project.version}"
        attributes['Implementation-Version' ] = project.version
        attributes['Implementation-Vendor'  ] = 'Luca Burgazzoli'
    }
}

test {
    testLogging {
        events "started", "passed", "skipped", "failed", "standardError"
    }
}


// *****************************************************************************
// ARTIFACTS
// *****************************************************************************

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier 'groovydoc'
    from groovydoc.destinationDir
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
    archives javadocJar
}

// *****************************************************************************
// Publish to gradle Plugins
// *****************************************************************************

pluginBundle {
    website     = project.gitProject
    vcsUrl      = project.gitProject
    description = project.name
    tags        = gradlePluginTags

    plugins {
        karafPlugin {
            id          = gradlePluginId
            displayName = project.description
        }
    }
}

// *****************************************************************************
// Publish to Sonatype OSS
// *****************************************************************************

signing {
    required {
        isReleaseVersion
    }

    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: project.nexusUrl) {
                authentication(
                    userName: project.nexusUsername,
                    password: project.nexusPassword
                )
            }

            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            pom.groupId = project.group
            pom.artifactId = project.name
            pom.version = project.version

            pom.project {
                name project.name
                description project.name
                url project.gitProject

                scm {
                    url project.gitProject
                    connection "scm:${project.gitProject}"
                    developerConnection "scm:${project.gitURL}"
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'lburgazzoli'
                        name 'Luca Burgazzoli'
                    }
                }
            }
        }
    }
}

// *****************************************************************************
//
// *****************************************************************************

afterReleaseBuild.dependsOn publishPlugins, uploadArchives

task ci(dependsOn: project.gradleCiTasks) {
    doFirst {
        println "=============================================================="
        println "CI Tasks: ${project.gradleCiTasks}"
        println "=============================================================="
    }
}

task close(dependsOn: 'closeAndReleaseRepository') {
}
